dnl Top-level configure for kpathsea-using programs.
dnl Our job is to run configure in the relevant subdirectories.
dnl
dnl Assumption: each program will create a c-auto.h that defines
dnl everything kpathsea needs.  We arrange this by including (at
dnl autoconf time) kpathsea/common.ac.
dnl 
dnl Consequence: we don't need to run configure in the kpathsea directory
dnl itself; we can just copy from a program directory.  We'll use the
dnl first one.
dnl 
AC_INIT(kpathsea/kpathsea.texi)

dnl These configure scripts won't work with anything else.
AC_PREREQ(2.12.1)

AC_PROG_MAKE_SET

dnl Because we include make/common.make.
AC_PROG_CC
AC_ISC_POSIX
AC_PROG_LN_S

dnl If we guessed the prefix in the subdirectories, it might guess
dnl differently than the prefix here, and that would cause confusion.
AC_PREFIX_PROGRAM(tex)

dnl Pass this down from the top level so that when the subprograms
dnl install kpathsea a command line value is propagated.
AC_PROG_INSTALL

dnl If the other programs ever acquire their own --with or --enable
dnl options, they must be added, so configure --help at the top level
dnl will show all the options.
sinclude(acklibtool.m4)
sinclude(withenable.ac)
sinclude(kpathsea/withenable.ac)
sinclude(web2c/withenable.ac)
sinclude(xdvik/withenable.ac)
sinclude(libwww/withenable.ac)

if test "x$with_omega" = xno; then
  with_odvipsk=no
  with_oxdvik=no
fi

ESUBDIRS=
DSUBDIRS=
for pkg in dtl dvidvi dviljk dvipsk gsftopk lacheck makeindexk musixflx \
    odvipsk oxdvik ps2pkm seetexk tetex tex4htk xdvik; do
  if eval "test \"x`echo '$with_'${pkg}`\" = xno"; then
    DSUBDIRS="$DSUBDIRS $pkg"
  else
    ESUBDIRS="$ESUBDIRS $pkg"
  fi
done

AC_SUBST(ESUBDIRS)
AC_SUBST(DSUBDIRS)

AC_CONFIG_SUBDIRS(kpathsea web2c dtl dvidvi dviljk dvipsk gsftopk lacheck \
                  libwww makeindexk musixflx odvipsk oxdvik ps2pkm seetexk \
                  tetex tex4htk xdvik)

AC_OUTPUT(Makefile)
